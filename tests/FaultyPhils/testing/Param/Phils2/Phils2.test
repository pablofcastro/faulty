// This version of the faulty philosophers use only two bits to codify the state
// of a philosopher, this reduces the state space of the examples
// a enumeration type will be added in next versions of the tool
// !s0!s1 : thinking
// !s0 s1: hungry
// s0!s1: eating
// s0s1: faulty

Global fork0 : BOOL;
Global fork1 : BOOL;

Process OddPhil(forkL: BOOL, forkR:BOOL){
   
   s0, s1, hasL, hasR: BOOL;
   Initial: !s0 && !s1  && !hasL && !hasR && forkR && forkL;
   Normative: !(s0 && s1); 

   !s0 && !s1 -> s0=false, s1=true; 
   !s0 && !s1 -> s0=false, s1=false;
   !s0 && s1 && forkL && !hasL && !hasR  -> forkL=false, hasL=true; 
   !s0 && s1 && !forkL && !hasL && !hasR -> s0=s0, s1=s1;
   !s0 && s1 && hasL && forkR && !hasR -> forkR = false, hasR=true;
   !s0 && s1 && hasL && !forkR && !hasR -> forkL = true, hasL=false; 
   !s0 && s1 && hasL && hasR -> s0=true, s1=false; 
   s0 && !s1 -> s0=false, s1=false, forkL=true, forkR=true, hasR=false, hasL=false;
   true -> s0=true, s1=true;   
}

Process EvenPhil(forkL: BOOL, forkR:BOOL){
   
   s0, s1, hasL, hasR: BOOL;
   Initial: !s0 && !s1  && !hasL && !hasR && forkR && forkL;
   Normative: !(s0 && s1); 

   !s0 && !s1 -> s0=false, s1=true; 
   !s0 && !s1 -> s0=false, s1=false;
   !s0 && s1 && forkR && !hasL && !hasR  -> forkR=false, hasR=true; 
   !s0 && s1 && !forkR && !hasR && !hasL -> s0=s0, s1=s1;
   !s0 && s1 && hasR && forkL && !hasL -> forkL = false, hasL=true;
   !s0 && s1 && hasR && !forkL && !hasL -> forkR = true, hasR=false; 
   !s0 && s1 && hasL && hasR -> s0=true, s1=false; 
   s0 && !s1 -> s0=false, s1=false, forkL=true, forkR=true, hasR=false, hasL=false;
   true -> s0=true, s1=true; 
}



Main(){
 phil1:OddPhil;
 phil2:EvenPhil;

 run phil1(fork1, fork0);
 run phil2(fork0, fork1);
}
